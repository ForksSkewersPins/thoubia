title: The Right is expelling us from their government!
subtitle: We have angered our coalition partners too much...
new-page: true
on-arrival: tried_supporting_kpd = 0; z_relation -= 6; dvp_relation -= 6; ddp_relation -= 6; lvp_relation -= 6; lvp_right += 3; dvp_right += 3; ddp_right += 3
view-if: chancellor_party != "SAPT" and chancellor_party != "KPD" and sapt_in_government = 1 and ((in_grand_coalition = 1 or in_popular_front = 1 or in_minority_government = 1) and coalition_dissent >= 3) or (in_weimar_coalition = 1 and coalition_dissent >= 4) and spd_r < 50
tags: event
go-to: menu if chancellor != "Wirth"; wirth_resign_hindenburg if chancellor == "Wirth" and president_ideology != "Left"; wirth_resign_braun if chancellor == "Wirth" and president_ideology == "Left"

= Cabinet Reshuffled

@menu

Chancellor [+ chancellor +] has announced his intentions to President [+ president +] to reshuffle the cabinet, sacking and replacing all current <span style="color: #c00000;">**SPD**</span> ministers and replacing them with bourgeois ones. [? if bourgeois_coalition < 50 and (right_coalition < 50 or dnvp_ideology == "Radical" and not dnvp_westarp) and (center_right_coalition < 50 or dnvp_ideology == "Radical") : Luckily, they lack a majority without us, so we can bring them down with a simple motion of no confidence. ?]

- @try_assuage: Try to assuage our partners.
- @minority_bruning: Brüning's cabinet survives as a minority government.
- @breitscheid_vonc: Call a constructive motion of no confidence with the <span style="color: #700000;">**KPD**</span>!
- @sack_bruning: President [+ president +] will call snap elections.
- @bruning_chain: What's next?
- @tolerate: Let the reshuffle happen, and tolerate the new minority government.
- @no_tolerate: Bring down this new backstabbing government!

@try_assuage

How do we assuage our partners?

- @dvp_prussia_2: Let the <span style="color: #C0A054;">**DVP**</span> into Prussia.
- @give_up_prussia: Give up on leadership of Prussia.
- @austerity: Enact a massive austerity plan.
- @resources: Use our resources to reduce dissent within the coalition.
- @menu: Never mind. What are our options?

@dvp_prussia_2
view-if: year <= 1929 and in_grand_coalition_prussia = 0 and (in_weimar_coalition_prussia or in_minority_weimar_coalition_prussia) and in_grand_coalition = 1 and dvp_was_in_prussia == 0 and not lvp_formed
choose-if: z_leader != "Kaas"
unavailable-subtitle: [? if z_leader = "Kaas": Kaas and the [? if z_party_name != "CVP": <span style="color: #000000;">Center</span>?][? if z_party_name == "CVP": <span style="color: #000000;">**CVP**</span>?] strongly dissents against. ?]
on-arrival: in_right_coalition_prussia = 0; in_weimar_coalition_prussia = 0; in_grand_coalition_prussia = 1; in_minority_grand_coalition_prussia = 0; in_popular_front_prussia = 0; in_left_front_prussia = 0; in_spd_majority_prussia = 0; in_center_right_coalition_prussia = 0; in_far_right_coalition_prussia = 0; in_minority_weimar_coalition_prussia = 0; ddp_in_popular_front_prussia = 0; kpd_goals_seen_prussia = 0; sapt_toleration_prussia = 0; dvp_relation += 10; z_relation -= 5; ddp_relation += 3; coalition_dissent -= 1; left_dissent += 5; in_grand_coalition_prussia = 1; dvp_left += 3; new_middle_dvp += 3

Letting the <span style="color: #C0A054;">**DVP**</span> into the Prussian government is an offer too good for them to refuse, no matter their leadership. 

However, this has negatively impacted our freedom of action we previously had in the <span style="color: #000000;">We</span><span style="color: #DD0000;">im</span><span style="color: #FFCC00;">ar</span> coalition.

@give_up_prussia
subtitle: Braun would need to resign.
view-if: prussia_leader = "Braun" and neo_weimar_coalition_prussia >= 50
on-arrival: coalition_dissent = 1; prussia_leader = "Stegerwald"; prussia_leader = "Brüning" if z_leader == "Stegerwald"; reformist_dissent += 10; center_dissent += 10; labor_dissent += 10; left_dissent += 5; workers_spd -= 5; new_middle_spd -= 5; rural_spd -= 3; braun_resign = 1

Fine. We no longer control the government of Prussia. The vote has been called off.

The Minister-President position is transferred to the [? if z_party_name != "CVP": <span style="color: #000000;">Center</span>?][? if z_party_name == "CVP": <span style="color: #000000;">**CVP**</span>?].

@austerity
view-if: welfare >= 0
on-arrival: coalition_dissent = 0; workers_spd -= 10; unemployed_spd -= 10; workers_kpd += 7; workers_nsdap += 3; unemployed_kpd += 5; unemployed_nsdap += 5; left_dissent += 15; center_dissent += 10; labor_dissent += 15; reformist_dissent += 5; welfare -= 2; budget += 1; unemployed += 3; kpd_coalition_dissent += 1; kpd_relation -= 10 if in_popular_front; goal_spd_cancel += 1

Fine. We enacted a massive austerity program to cut welfare, in order to maintain the stability of our coalition. The vote has been called off.

@resources
choose-if: (resources >= 3 and in_weimar_coalition or in_popular_front or in_minority_government) or ((resources >= 3 and in_grand_coalition and not (dvp_ideology == "Right" and year >= 1930) and not lvp_formed) or (resources >= 3 and (in_grand_coalition or in_weimar_coalition) and not (lvp_ideology == "Right" and year >= 1930) and lvp_formed))
unavailable-subtitle: [? if resources < 3: We do not have enough resources. ?] [? if in_grand_coalition and (dvp_ideology == "Right" and year >= 1930) and not lvp_formed: The <span style="color: #C0A054;">**DVP**</span> doesn't want our resources anymore. ?] [? if (in_grand_coalition or in_weimar_coalition) and (lvp_ideology == "Right" and year >= 1930) and lvp_formed: The <span style="color: #FFCC00;">**LVP**</span> doesn't want our resources anymore. ?]
subtitle: -3 resources
on-arrival: resources -= 3; coalition_dissent -= 1

By transferring resources to our coalition partners, we can reduce their dissent against our policies. This is a crude, but effective solution.

@minority_bruning
view-if: constructive_vonc and not (right_coalition >= 50 and bourgeois_coalition < 50 and (dnvp_ideology == "Moderate" or dnvp_westarp) and not (year = 1929 and month >= 7)) and not (right_coalition < 50 and center_right_coalition >= 50 and bourgeois_coalition < 50 and dnvp_ideology == "Moderate" and not (year = 1929 and month >= 7))
on-arrival: sapt_in_government = 0; minority_government = 1; grand_coalition_collapsed = 1 if in_grand_coalition; in_grand_coalition = 0; in_weimar_coalition = 0; coalition_dissent = 0; chancellor = "Brüning"; chancellor_party = "Z"; foreign_minister_party = "DVP"; foreign_minister_party = "LVP" if lvp_formed; interior_minister_party = "Z"; justice_minister_party = "I"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = ddp_name; economic_minister_party = "LVP" if lvp_formed; finance_minister_party = "DVP"; finance_minister_party = "LVP" if lvp_formed; agriculture_minister_party = "Z"; lvp_right += 3; ddp_right += 3; ddp_cohesion -= 1; dvp_right += 3

Due to our own constitutional reforms, we cannot bring Brüning down. We will have to prepare for the next election to get our chance.

@breitscheid_vonc
view-if: constructive_vonc and not (right_coalition >= 50 and bourgeois_coalition < 50 and (dnvp_ideology == "Moderate" or dnvp_westarp) and not (year = 1929 and month >= 7)) and not (right_coalition < 50 and center_right_coalition >= 50 and bourgeois_coalition < 50 and dnvp_ideology == "Moderate" and not (year = 1929 and month >= 7)) and (left_coalition >= 50 or kpd_relation >= 50)
choose-if: left_coalition >= 50 and kpd_relation >= 50
unavailable-subtitle: [? if left_coalition < 50 : We do not have a majority in favor of Breitscheid. ?][? if kpd_relation < 50 : The Communists are unwilling. ?]
on-arrival: in_left_front = 1; in_grand_coalition = 0; in_weimar_coalition = 0; in_popular_front = 0; chancellor_party = "SAPT"; chancellor = "Breitscheid"; coup_progress += 3; capital_strike_progress += 2; coup_progress += 1 if in_left_front_prussia; capital_strike_progress += 1 if in_left_front_prussia; labor_minister_party = "SAPT"; interior_minister_party = "SAPT"; finance_minister_party = "SAPT"; economic_minister_party = "SAPT"; justice_minister_party = "SAPT"; foreign_minister_party = "SAPT"; agriculture_minister_party = "SAPT"; reichswehr_minister_party = "SAPT"; kpd_relation += 10; z_relation -= 5; ddp_relation -= 5; dvp_relation -= 10; lvp_relation -= 8; reichswehr_loyalty -= 0.05; dvp_right += 10; ddp_right += 5; ddp_cohesion -= 3; lvp_right += 8; liberal_cooperation += 5; bourgeois_cooperation += 10; liberal_cooperation += 3 if in_left_front_prussia or in_popular_front_prussia; bourgeois_cooperation += 5 if in_left_front_prussia or in_popular_front_prussia; reformist_dissent += 20; reformist_strength -= 10; left_dissent -= 30; left_strength += 20

We have successfully passed a motion of no confidence to replace Brüning with Breitscheid. We are now ruling in a united front with the <span style="color: #700000;">**KPD**</span>.

Whatever their reasoning is, the <span style="color: #c00000;">**SPD**</span> has control over the ministries of government.

@sack_bruning
view-if: constructive_vonc and president_ideology == "Left"
choose-if: presidential_powers != 1
unavailable-subtitle: We don't have that power anymore. You really screwed us here with your reforms, you know that?
call: set_next_election_time
on-arrival: time_to_election = 3; sapt_in_government = 0; in_grand_coalition = 0; in_weimar_coalition = 0; in_popular_front = 0; sapt_toleration = 0; sapt_toleration_right = 0; coalition_dissent = 0; pro_republic -= 5; old_middle_spd -= 3; new_middle_spd -= 2; workers_spd -= 3 if pro_republic < 50; workers_spd -= 4 if pro_republic < 40; old_middle_nsdap += 4 if pro_republic < 50; new_middle_nsdap += 3 if pro_republic < 50; lvp_right += 3; ddp_right += 2; ddp_cohesion -= 1; dvp_right += 3

We have sacked Brüning and called for new elections for [+ next_election_month : month +] [+ next_election_year +].

@bruning_chain
view-if: not constructive_vonc and not (right_coalition >= 50 and (dnvp_ideology == "Moderate" or dnvp_westarp) and not (year = 1929 and month >= 7)) and not ((center_right_coalition >= 50 and right_coalition < 50) and dnvp_ideology == "Moderate" and not (year = 1929 and month >= 7)) and (not bruning_event_seen and year >= 1930)
on-arrival: sapt_in_government = 0; minority_government = 1; grand_coalition_collapsed = 1 if in_grand_coalition; in_grand_coalition = 0; in_weimar_coalition = 0; coalition_dissent = 0; chancellor = "Brüning"; chancellor_party = "Z"; foreign_minister_party = "DVP"; foreign_minister_party = "LVP" if lvp_formed; interior_minister_party = "Z"; justice_minister_party = "I"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = ddp_name; economic_minister_party = "LVP" if lvp_formed; finance_minister_party = "DVP"; finance_minister_party = "LVP" if lvp_formed; agriculture_minister_party = "Z"; bruning_event_timer = 6 if not bruning_event_seen and year >= 1930

Replacing us is Brüning of the [? if z_party_name != "CVP": <span style="color: #000000;">Center Party</span>?][? if z_party_name == "CVP": <span style="color: #000000;">**CVP**</span>?], who has reorganized his cabinet to encompass the bourgeois parties. [? if not bruning_event_seen and year >= 1930: New elections haven't been called yet but it's likely that they will be soon, considering Brüning most likely doesn't have the majority of the Reichstag behind him. ?]

@tolerate
view-if: not constructive_vonc and (bruning_event_seen or year < 1930)
choose-if: not (right_coalition >= 50 and bourgeois_coalition < 50 and (dnvp_ideology == "Moderate" or dnvp_westarp) and not (year = 1929 and month >= 7)) and not (right_coalition < 50 and center_right_coalition >= 50 and bourgeois_coalition < 50 and dnvp_ideology == "Moderate" and not (year = 1929 and month >= 7))
unavailable-subtitle: They don't need us.
on-arrival: sapt_in_government = 0; grand_coalition_collapsed = 1 if in_grand_coalition; in_grand_coalition = 0; in_weimar_coalition = 0; coalition_dissent = 0; left_dissent += 15; labor_dissent += 5; center_dissent += 5; workers_spd -= 3; workers_kpd += 3; workers_nsdap += 3; lvp_left += 1; ddp_left += 0.5; dvp_left += 1
go-to: election_1928.toleration_bruning

Many in the party are incredulous. We have just been expelled from government, and our reaction is to allow this government to govern?

@no_tolerate
view-if: not constructive_vonc and (bruning_event_seen or year < 1930)
choose-if: not (right_coalition >= 50 and bourgeois_coalition < 50 and (dnvp_ideology == "Moderate" or dnvp_westarp) and not (year = 1929 and month >= 7)) and not (right_coalition < 50 and center_right_coalition >= 50 and bourgeois_coalition < 50 and dnvp_ideology == "Moderate" and not (year = 1929 and month >= 7))
unavailable-subtitle: They don't need us.
call: set_next_election_time
on-arrival: time_to_election = 3; sapt_in_government = 0; grand_coalition_collapsed = 1 if in_grand_coalition; in_grand_coalition = 0; in_weimar_coalition = 0; in_popular_front = 0; sapt_toleration = 0; sapt_toleration_right = 0; foreign_minister_party = "DVP"; foreign_minister_party = "LVP" if lvp_formed; interior_minister_party = "Z"; justice_minister_party = "I"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = ddp_name; economic_minister_party = "LVP" if lvp_formed; finance_minister_party = "DVP"; finance_minister_party = "LVP" if lvp_formed; agriculture_minister_party = "Z"; labor_minister = "Stegerwald"; interior_minister = "Wirth"; finance_minister = "Moldenhauer"; economic_minister = "Curtius"; justice_minister = "Koch-Weser"; foreign_minister = "Stresemann" if not stresemann_dead; foreign_minister = "Curtius" if stresemann_dead; agriculture_minister = "Dietrich"; reichswehr_minister = "Groener"; coalition_dissent = 0; pro_republic -= 4; old_middle_spd -= 3; new_middle_spd -= 2; workers_spd -= 3 if pro_republic < 50; workers_spd -= 4 if pro_republic < 40; old_middle_nsdap += 4 if pro_republic < 50; new_middle_nsdap += 3 if pro_republic < 50; welfare -= 2

The vote succeeds, and their government has resigned. Chancellor Brüning remains as the head of an acting government, consisting solely of the bourgeois parties. He immediately proceeds to cut welfare spending substantially.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@wirth_resign_braun
call: set_next_election_time
on-arrival: time_to_election = 3; chancellor = "Wels"; chancellor_party = "SAPT"; spd_caretaker = 1; grand_coalition_collapsed = 1 if in_grand_coalition; in_grand_coalition = 0; in_weimar_coalition = 0; in_popular_front = 0; sapt_toleration = 0; sapt_toleration_right = 0; foreign_minister_party = "I"; interior_minister_party = "I"; justice_minister_party = "I"; labor_minister_party = "I"; reichswehr_minister_party = "I"; economic_minister_party = "I"; finance_minister_party = "I"; agriculture_minister_party = "I"; coalition_dissent = 0; pro_republic -= 5

Unable to bear the stress of keeping the coalition together, Wirth has announced his intentions to resign as chancellor.

[+ president +] has appointed as replacement Wels, who is immediately faced with a successful motion of no confidence. New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@wirth_resign_hindenburg
on-arrival: chancellor = "Brüning"; chancellor_party = "Z"

Unable to bear the stress of keeping the coalition together, Wirth has announced his intentions to resign as chancellor.

[+ president +] appoints Brüning from the [? if z_party_name != "CVP": <span style="color: #000000;">Center Party</span>?][? if z_party_name == "CVP": <span style="color: #000000;">**CVP**</span>?] as replacement, who immediately reforms the government.

- @menu: Brüning sacks our ministers.
